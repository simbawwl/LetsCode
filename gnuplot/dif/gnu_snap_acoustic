unset key
set macros
### set output format
#set terminal pdfcairo
c=5
set terminal postscript eps size 37.0/c,9.0/c font "Arial, 16"
set output sprintf("snap.eps")
set format cb "%1.2f"
### set color
set palette file "-"
0 0 1
1 1 1
1 0 0
e
#set colorbox
### set xy label 
set yrange [1:256] reverse
### plot range and tics
### transpose data
!transp n1=256 <snap_h_1>input_h_1.bin
!transp n1=256 <snap_h_2>input_h_2.bin
!transp n1=256 <snap_h_3>input_h_3.bin
!transp n1=256 <snap_h_4>input_h_4.bin
!transp n1=256 <snap_h_5>input_h_5.bin
NOTICS="unset title; unset xlabel;unset ylabel;unset xtics;unset ytics; unset x2tics; unset y2tics; unset colorbox"
TICS="set xlabel 'X position (km)'; set ylabel 'Depth (km)'; set xtics('0.0' 0.0,'1.0' 64.0,'2.0' 128.0,'3.0' 192.0,'4.0' 255.0) in; set ytics('0.0' 0,'1.0' 64.0,'2.0' 128.0,'3.0' 192.0,'4.0' 255.0) in; set grid front; unset grid"
vsize=0.35*2
hsize=0.086*2
lmarg=0.07
tmarg=0.88
mmarg=0.08
TMARGIN = "set tmargin at screen tmarg; set bmargin at screen tmarg-vsize"
BMARGIN = "set tmargin at screen tmarg-vsize-mmarg; set bmargin at screen tmarg-vsize-mmarg-vsize"
LMARGIN1 = "set lmargin at screen lmarg; set rmargin at screen lmarg+hsize"
LMARGIN2 = "set lmargin at screen lmarg+hsize*1; set rmargin at screen lmarg+hsize*2"
LMARGIN3 = "set lmargin at screen lmarg+hsize*2; set rmargin at screen lmarg+hsize*3"
LMARGIN4 = "set lmargin at screen lmarg+hsize*3; set rmargin at screen lmarg+hsize*4"
LMARGIN5 = "set lmargin at screen lmarg+hsize*4; set rmargin at screen lmarg+hsize*5"

#set style line 1 lt 2 lc rgb "black" lw 3
@NOTICS
### plot
#r=0.02 #for vector 
r=0.2 # for div cul
set cbrange [-1*r:1*r]
set multiplot layout 1,5 rowsfirst
#set title 'Horizontal Component'
@TICS;unset xlabel;
@TMARGIN; @LMARGIN1
set title "T=0.1 (s)"
plot "input_h_1.bin" binary array=256x256 format="%float" with image

@NOTICS
@TMARGIN; @LMARGIN2
set title "T=0.2 (s)"
plot "input_h_2.bin" binary array=256x256 format="%float" with image
@TMARGIN; @LMARGIN3
set title "T=0.3 (s)"
plot "input_h_3.bin" binary array=256x256 format="%float" with image
@TMARGIN; @LMARGIN4
set title "T=0.4 (s)"
plot "input_h_4.bin" binary array=256x256 format="%float" with image
@TMARGIN; @LMARGIN5
set colorbox 
#set cbtics('-1.0' -1.0,'-0.5' -0.5,'0.0' 0.0,'0.5' 0.5,'1.0' 1.0) # for vector
set title "T=0.5 (s)"
plot "input_h_5.bin" binary array=256x256 format="%float" with image
#
### delete temp files
!rm input_h_1.bin	
!rm input_h_2.bin	
!rm input_h_3.bin	
!rm input_h_4.bin	
!rm input_h_5.bin	
